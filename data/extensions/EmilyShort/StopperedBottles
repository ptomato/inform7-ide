Version 1/050412 of Stoppered Bottles by Emily Short begins here.

A bottle is a kind of container. Bottles are usually openable, transparent, and closed.

A cork is a kind of thing. A cork is in every bottle.

Understand "cork [something]" as corking.

Understand the command "stopper" as "cork".

Understand "uncork [something]" as uncorking.

Corking is an action applying to one thing.

Check corking:
	if the noun is not a bottle, say "[The noun] cannot be corked." instead.

Carry out corking:
	try closing the noun.

Uncorking is an action applying to one thing.

Check uncorking:
	if the noun is not a bottle, say "[The noun] cannot be uncorked." instead.
	
Carry out uncorking:
	try opening the noun.
	
	
	
Understand "close [something] with [something held]" as corking it with.

Understand "cork [something] with [something held]" as corking it with.

Corking it with is an action applying to two things.

Check corking it with:
	if the noun is not a bottle, say "[The noun] cannot be corked." instead;
	if the second noun is not a cork, say "[The second noun] will not fit in [the noun]." instead.

Carry out corking it with:
	try inserting the second noun into the noun instead.
	

Instead of closing a bottle:
	if a cork (called the item) is carried by the player, try inserting the item into the noun instead;
	otherwise say "You need a stopper of some kind."

Instead of opening a bottle:
	if a cork (called the item) is in the bottle, try taking the item instead;
	otherwise say "[The noun] has no stopper."

After inserting a cork into a bottle:
	change the second noun to closed;
	say "You stopper [the second noun] with [the noun]."
	
Before taking a cork when the noun is in a closed bottle (called the item):
	change the item to open;

Instead of taking a cork when the noun is in a bottle (called the item):
	move the noun to the player;
	say "You pull [the noun] from [the item]." instead.
	
After printing the name of a closed bottle while not inserting, searching, or removing: 
	if the bottle contains a cork (called the item), say " with [an item] in it"; 
	omit contents in listing.

Stoppered Bottles ends here.

<Documentation>

Stoppered Bottles provides two kinds, the bottle and the cork. Bottles begin the game corked. The player can remove the cork and put it back in, with appropriate effects for the open or closed state of the bottle. 

+	>open bottle
+	You pull the cork from the champagne bottle.

The player cannot close a bottle if he is not carrying a cork:

+	>close bottle
+	You need a stopper of some kind.

Corking also affects inventory listing. When a bottle is corked, it will be listed not as

+	a champagne bottle
+	+	a cork

but

+	a champagne bottle with a cork in it

To include a bottle in a game we need only say

+	The champagne bottle is a bottle in the Neat Room.

Finally, Stoppered Bottles adds CORK and UNCORK to the player's vocabulary. These commands affect only bottles, but are then treated as equivalent to CLOSE and OPEN.